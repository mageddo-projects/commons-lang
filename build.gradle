plugins {
  id 'java-library'
  id 'net.researchgate.release' version '3.0.2'
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

repositories {
  mavenCentral()
}

apply from: "${rootDir}/nexus.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileTestJava {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

dependencies {

  compileOnly 'org.projectlombok:lombok:1.18.26'
  annotationProcessor 'org.projectlombok:lombok:1.18.26'

  testCompileOnly 'org.projectlombok:lombok:1.18.26'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

  implementation('org.apache.commons:commons-lang3:3.12.0')
  testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
  testImplementation("org.mockito:mockito-junit-jupiter:5.1.1")
}

test {
  useJUnitPlatform()
}

release {

  project.ext.set("release.useAutomaticVersion", true)
  git {
    requireBranch.set("master")
  }
  failOnCommitNeeded = false
  failOnPublishNeeded = false
  failOnUnversionedFiles = false
  buildTasks = []
}

confirmReleaseVersion.doLast {
  def f = file("${project.projectDir}/README.md")
  def text = f.text
  f.withWriter { w -> w << text.replaceAll("(:\\ )*\\d+\\.\\d+\\.\\d+", "\$1${version}")
  }
}
